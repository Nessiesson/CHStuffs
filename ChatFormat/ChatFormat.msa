*:'/format' [$type=''] [$] = >>>
	@options = _chatformat_get_options();
	@type = to_lower($type);

	if(@type == '') {
		msg(colorize('&aYou can choose between the following options:'));
		foreach(@name: @description in @options) {
			msg(colorize('&a -&e ' . @name . '&a: ' . @description . '&a.'));
		}
		die();
	}
	if(!array_index_exists(@options, @type)) {
		die(colorize('&e' . @type . ' &cis not a valid formatting option.'));
	}
	msg(colorize('&e' . @type . '&a: ' . @options[@type] . '&a.'))
	msg(colorize('&aCurrently looks like:'));
	@format = associative_array(
		'type': @type,
		'player': player(),
		'message': 'This is a message.',
		'from': 'Notch',
		'to': player(),
	);
	msg(_chatformat_construct_message(player(), @format));
	# Adding a newline, because it looks nice.
	msg('');
<<<
